- [Input variables](#input-variables)
- [Variable types](#variable-types)
  - [Assigning default values](#assigning-default-values)
- [Type Constraints for Variables](#type-constraints-for-variables)
  - [Data Type Validation](#data-type-validation)
  - [Supported Data Types in Terraform](#supported-data-types-in-terraform)
  - [Examples: Variables with Type Validation](#examples-variables-with-type-validation)
  - [Example: Validating Input Variables](#example-validating-input-variables)
- [Passing Variables Securely](#passing-variables-securely)
  - [Passing variables from the command line](#passing-variables-from-the-command-line)
  - [Variable precedence](#variable-precedence)
  - [Using Environment Variables for Sensitive Data](#using-environment-variables-for-sensitive-data)
  - [Dynamic Variables](#dynamic-variables)
- [Local Values](#local-values)
- [Dynamic Blocks](#dynamic-blocks)
- [Conditional Logic](#conditional-logic)
  - [Ternary Operators](#ternary-operators)
  - [Conditional Resource Creation](#conditional-resource-creation)
- [Meta-Arguments](#meta-arguments)
- [Splat Expressions](#splat-expressions)
- [Functions](#functions)
  - [Built-in Functions in Terraform](#built-in-functions-in-terraform)
  - [Using Functions to Manipulate Data](#using-functions-to-manipulate-data)
- [Managing different environments](#managing-different-environments)
- [Complex Lists and Maps](#complex-lists-and-maps)
- [Resource Dependencies Clarification](#resource-dependencies-clarification)
- [Using Provisioners for Post-Deployment Actions](#using-provisioners-for-post-deployment-actions)


## Input variables

- Variables allow you to make your Terraform configurations flexible and reusable by letting users input values without hardcoding them.
- You define variables in a separate block and reference them in your resource definitions.
- Example
    ```hcl
    variable "instance_type" {
        default = "t2.micro"
    }

    resource "aws_instance" "example" {
        instance_type = var.instance_type
    }
    ```

## Variable types
- **String**: A single text value.
- **List**: A sequence of values.
- **Map**: A set of key-value pairs.
- Example: variable definitions
    ```hcl
    variable "instance_name" {
        type    = string
        default = "example-instance"
    }

    variable "allowed_ports" {
        type    = list
        default = [80, 443, 8080]
    }

    variable "instance_tags" {
        type = map
        default = {
            Name        = "example-instance"
            Environment = "dev"
        }
    }

    variable "create_instance" {
        type    = bool
        default = true
    }

    variable "instance_count" {
        type    = number
        default = 3
    }
    ```
- Example: variable usage in resources
    ```hcl
    resource "aws_security_group" "example" {
        name = var.instance_name

        ingress {
            from_port   = var.allowed_ports[0]
            to_port     = var.allowed_ports[0]
            protocol    = "tcp"
            cidr_blocks = ["0.0.0.0/0"]
        }

        ingress {
            from_port   = var.allowed_ports[1]
            to_port     = var.allowed_ports[1]
            protocol    = "tcp"
            cidr_blocks = ["0.0.0.0/0"]
        }
    }

    resource "aws_instance" "example" {
        count           = var.create_instance ? var.instance_count : 0
        ami             = "ami-0c55b159cbfafe1f0"
        instance_type   = "t2.micro"
        tags            = var.instance_tags
    }
    ```

### Assigning default values
- You can assign default values to variables to make your configuration more flexible, allowing users to override those values only when necessary
- Example
    ```hcl
    variable "region" {
        default = "us-west-2"
    }
    ```

## Type Constraints for Variables

### Data Type Validation
- Terraform supports type constraints beyond just string, list, and map.
- You can enforce more specific types such as list(string) or map(number).
- Adding this to your configuration ensures that inputs follow a stricter format.
- Example
    ```hcl
    variable "instance_tags" {
        type = map(string)
    }
    ```

### Supported Data Types in Terraform
- Primitive Types
    - **String:** A single string of text (e.g., "example-instance").
    - **Number:** Integer or floating-point number (e.g., 3, 5.2).
    - **Bool:** Boolean value (true or false). 
- Complex Types
    - **List(T):** An ordered sequence of values, all of the same type (e.g., list(string)).
    - **Set(T):** An unordered collection of unique values (e.g., set(string)).
    - **Map(T):** A collection of key-value pairs, where all keys are strings and all values are of a specific type (e.g., map(number)).
    - **Object:** A map with predefined keys of different types (e.g., object({ name = string, age = number })).
    - **Tuple:** An ordered sequence of values with possibly different types (e.g., tuple([string, number])).

### Examples: Variables with Type Validation
```hcl
# String Variable with Type Validation
variable "instance_name" {
    type    = string
    default = "example-instance"
}

# List Variable with Type Validation
variable "allowed_ports" {
    type = list(string)
    default = ["80", "443"]
}

# Map Variable with Type Validation
variable "instance_tags" {
    type = map(string)
    default = {
        Name        = "example-instance"
        Environment = "production"
    }
}

# Object Type
variable "instance_details" {
    type = object({
        instance_type = string
        instance_count = number
    })
    default = {
        instance_type  = "t2.micro"
        instance_count = 3
    }
}

# Tuple Type
variable "resource_data" {
    type = tuple([string, number, bool])
    default = ["web-server", 2, true]
}

# List of Objects
variable "security_groups" {
    type = list(object({
        name   = string
        port   = number
        cidr   = string
    }))
    default = [
        { name = "http", port = 80, cidr = "0.0.0.0/0" },
        { name = "https", port = 443, cidr = "0.0.0.0/0" }
    ]
}
```

### Example: Validating Input Variables
```hcl
# Validating a Number
variable "instance_count" {
    type = number
    validation {
        condition     = var.instance_count >= 1 && var.instance_count <= 10
        error_message = "The instance_count must be between 1 and 10."
    }
    default = 3
}

# Validating a String with a Regex
variable "environment_name" {
    type = string
    validation {
        condition     = can(regex("^prod-", var.environment_name))
        error_message = "The environment name must start with 'prod-'."
    }
}
```

## Passing Variables Securely

### Passing variables from the command line
- You can override variables using the -var flag or by specifying a .tfvars file when running Terraform commands
- Example
    ```hcl
    terraform apply -var="instance_type=t2.medium"
    ```

### Variable precedence
- Environment variables
- Command-line flags
- `.tfvars` files
- Default values in the configuration file

### Using Environment Variables for Sensitive Data
- Although .tfvars are useful, sensitive data like API keys and passwords should ideally be managed using environment variables or tools like `Terraform Cloud Vault`.
- Example
    ```hcl
    export TF_VAR_aws_access_key="your-access-key"
    export TF_VAR_aws_secret_key="your-secret-key"
    ```

- Terraform has a data source that allows you to securely pull values from `AWS Secrets Manager`, keeping your sensitive information outside your configuration files.
- **Step1: Configure AWS Provider**
    ```hcl
    provider "aws" {
        region = "us-west-2"
    }
    ```
- **Step2: Fetch the Secret Using Terraform Data Source:**
- Use Terraform’s aws_secretsmanager_secret and aws_secretsmanager_secret_version data sources to retrieve the secret values.
    ```hcl
    # Fetch the Secret metadata (ID, name, etc.)
    data "aws_secretsmanager_secret" "db_password" {
        name = "prod/db-password"
    }

    # Fetch the latest version of the secret
    data "aws_secretsmanager_secret_version" "db_password_version" {
        secret_id = data.aws_secretsmanager_secret.db_password.id
    }
    ```
- **Step3: Use the Secret in Resources:**
    ```hcl
    resource "aws_rds_instance" "example" {
        allocated_storage    = 20
        engine               = "mysql"
        engine_version       = "8.0"
        instance_class       = "db.t2.micro"
        name                 = "mydb"
        username             = "admin"
        password             = data.aws_secretsmanager_secret_version.db_password_version.secret_string  # Fetching the password from AWS Secrets Manager
        skip_final_snapshot  = true
    }
    ```

### Dynamic Variables
- Sometimes, it’s helpful to calculate values dynamically based on other inputs or configurations. You can use the locals feature to declare dynamic values.
- Example
    ```hcl
    locals {
        instance_count = var.is_production ? 5 : 1
    }
    ```


<!-- TODO: Content of `Efficient Resource Configuration` file -->

## Local Values
- Local values allow you to assign a name to an expression so that you can reuse that expression multiple times throughout your configuration.
- They help to avoid repetition and make the configuration easier to maintain.
- Local values are defined using the locals block
- They are accessible throughout the configuration, use them by referencing the local name.
- Example
    ```hcl
    locals {
        instance_type = "t2.micro"
        instance_name = "web-server"
    }

    resource "aws_instance" "example" {
        instance_type = local.instance_type
        tags = {
            Name = local.instance_name
        }
    }
    ```

## Dynamic Blocks
- Dynamic blocks are used in situations where the number of resource blocks or nested configurations may vary based on variables or conditions.
- They allow you to generate multiple blocks dynamically inside a resource.
- Dynamic blocks are commonly used when you have to generate similar blocks based on dynamic inputs, such as defining multiple security group rules or configuring multiple load balancers.
- The dynamic block is used to iterate over a list or map to generate multiple instances of a nested block.
- Example
    ```hcl
    # two security rules defined in the security_rules variable
    variable "security_rules" {
        default = [
            { port = 80, protocol = "tcp" },
            { port = 443, protocol = "tcp" }
        ]
    }

    # The dynamic block iterates over each rule to create an ingress block for each one.
    resource "aws_security_group" "example" {
        name = "example"

        dynamic "ingress" {
            for_each = var.security_rules
            content {
                from_port   = ingress.value.port
                to_port     = ingress.value.port
                protocol    = ingress.value.protocol
                cidr_blocks = ["0.0.0.0/0"]
            }
        }
    }
    ```

## Conditional Logic
- Conditional logic allows you to decide which value or resource configuration to use based on a condition.
- This is typically done using the ternary operator.
- Example
    ```hcl
    variable "is_production" {
        type    = bool
        default = false
    }

    resource "aws_instance" "example" {
        instance_type = var.is_production ? "t2.large" : "t2.micro"
    }
    ```

### Ternary Operators
- Structure
    ```hcl
    condition ? true_value : false_value
    ```

### Conditional Resource Creation
- You can conditionally create resources based on variables by using count or for_each.
- If the condition results in 0, the resource is not created.
- Example
    ```hcl
    resource "aws_instance" "example" {
        count         = var.create_instance ? 1 : 0
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
    }
    ```

## Meta-Arguments
- Meta-arguments like count, for_each, and depends_on are arguments that apply to all resource types and control their lifecycle, iteration, and dependencies.
- `count`: This meta-argument creates multiple instances of a resource based on a single value or condition.
    ```hcl
    resource "aws_instance" "example" {
        count         = 3
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
    }
    ```
- `for_each`: This meta-argument creates multiple resources based on a map or set of strings. It is often more flexible than count.
    ```hcl
    variable "servers" {
        default = {
            web1 = "ami-0c55b159cbfafe1f0"
            web2 = "ami-0c55b159cbfafe1f0"
        }
    }

        resource "aws_instance" "example" {
        for_each = var.servers
        ami           = each.value
        instance_type = "t2.micro"
        tags = {
            Name = each.key
        }
    }
    ```
- `depends_on`: Defines explicit dependencies between resources, ensuring Terraform applies resources in the correct order.
    ```hcl
    resource "aws_instance" "example" {
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
        depends_on    = [aws_security_group.example]
    }
    ```
- `lifecycle`: is used to customize how Terraform handles the creation, update, and deletion of resources. It provides additional control over the resource’s lifecycle.
    - Example: managing an EC2 instance that stores files in an S3 bucket
    ```hcl
    provider "aws" {
        region = "us-west-2"
    }

    # S3 Bucket Resource (Cannot be destroyed)
    resource "aws_s3_bucket" "important_bucket" {
        bucket = "my-important-bucket"
        
        # Lifecycle configuration
        lifecycle {
            prevent_destroy = true              # Prevents S3 bucket from being destroyed, even if someone tries to run terraform destroy or modify the configuration to remove the resource.
        }
    }

    # EC2 Instance Resource
    resource "aws_instance" "example" {
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
        tags = {
            Name = "Important Server"
        }

        # Lifecycle configuration
        lifecycle {
            create_before_destroy = true      # Ensures new instance is created before destroying the old one, ensuring there is no downtime.
            ignore_changes = [instance_type]  # Ignores manual changes to `instance_type` made outside of Terraform
            replace_triggered_by = [ami]      # Forces the EC2 instance to be replaced if the AMI ID changes, ensuring that any change in the AMI results in a new instance being created.
        }
    }
    ```
- `provider`: allows you to explicitly specify which provider configuration a resource should use. This is especially useful when working with multiple provider configurations or different environments
    ```hcl
    provider "aws" {
        alias  = "us_west"
        region = "us-west-1"
    }

    provider "aws" {
        alias  = "us_east"
        region = "us-east-1"
    }

    resource "aws_instance" "example_us_west" {
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
        provider      = aws.us_west  # Explicitly using the us-west provider
    }

    resource "aws_instance" "example_us_east" {
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
        provider      = aws.us_east  # Explicitly using the us-east provider
    }
    ```

## Splat Expressions
- Splat expressions allow you to access multiple attributes of a resource or variable when working with a list or map of values.
- When a resource or output generates multiple values (e.g., when creating multiple instances using count), you can use a splat expression to access all of them at once.
    ```hcl
    resource "aws_instance" "example" {
        count         = 3
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"
    }

        output "instance_ids" {
        value = aws_instance.example[*].id  # Splat expression to get all instance IDs
    }
    ```
- Splat expressions simplify accessing multiple attributes from resources, making it easier to work with resources that have multiple instances.

<!-- TODO: Content of `Data Manipulation` file -->
## Functions
- in-built functions
- user-defined function
    ```hcl
    locals {
        calculate_storage = local.instance_size == "large" ? 100 : 50
    }

    variable "instance_size" {
        type    = string
        default = "small"
    }

    resource "aws_instance" "example" {
        instance_type = var.instance_size
        root_block_device {
            volume_size = local.calculate_storage
        }
    }
    ```

### Built-in Functions in Terraform

### Using Functions to Manipulate Data

<!-- TODO: Content of Other files -->


## Managing different environments
- You can manage different environments (like dev, stage, prod) by using multiple .tfvars files (e.g., dev.tfvars, prod.tfvars) and specify which file to use when applying configurations.
- Example
    ```hcl
    terraform apply -var-file="dev.tfvars"
    ```
- allows easily switching between environments while keeping the infrastructure code clean and consistent.

## Complex Lists and Maps
- In case of more complex infrastructure, you might need to use nested lists or maps.
- Terraform supports these nested types, which can help when working with complex networking or resource allocation scenarios.
- Example
    ```hcl
    variable "allowed_ports" {
        type = list(object({
            from_port = number
            to_port   = number
            protocol  = string
        }))
        default = [
            { from_port = 80, to_port = 80, protocol = "tcp" },
            { from_port = 443, to_port = 443, protocol = "tcp" }
        ]
    }
    ```

## Resource Dependencies Clarification
- While the `depends_on` attribute is useful, Terraform’s implicit dependency management (based on resource references) is often sufficient.
- Example: if resource `A` uses the output of resource `B`, Terraform will automatically understand the dependency, so `depends_on` is rarely needed.
    ```hcl
    resource "aws_instance" "example" {
        ami           = aws_ami.my_ami.id  # Implicit dependency on aws_ami
        instance_type = "t2.micro"
    }
    ```

## Using Provisioners for Post-Deployment Actions
- Although provisioners were discouraged earlier, it’s still useful to understand their limitations.
- If you need to use them, make sure it’s only for actions like bootstrapping an instance (e.g., running a script after an instance is created).
- Example
    ```hcl
    resource "aws_instance" "example" {
        ami           = "ami-0c55b159cbfafe1f0"
        instance_type = "t2.micro"

        provisioner "remote-exec" {
            inline = [
            "sudo apt-get update",
            "sudo apt-get install -y nginx"
            ]
        }
    }
    ```